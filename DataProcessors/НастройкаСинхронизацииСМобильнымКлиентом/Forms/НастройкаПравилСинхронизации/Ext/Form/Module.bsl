
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Параметры.Свойство("ТекущийПользователь", ТекущийПользователь);

	Если Не ЗначениеЗаполнено(ТекущийПользователь) Тогда
		ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;

	Элементы.ТекущийПользователь.Видимость = РольДоступна(Метаданные.Роли.ПолныеПрава);

	ЗагрузитьНастройки();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекущийПользовательПриИзменении(Элемент)

	ЗагрузитьНастройки();

КонецПроцедуры

&НаКлиенте
Процедура ТекущийПользовательОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ФильтроватьПередаваемыеНаКлиентФайлыПриИзменении(Элемент)

	Элементы.ФорматыПередаваемыхФайлов.Доступность = ФильтроватьПередаваемыеНаКлиентФайлы;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)

	Если Не ЗначениеЗаполнено(ТекущийПользователь) Тогда

		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле  = "ТекущийПользователь";
		Сообщение.Текст = "Не выбран пользователь";
		Сообщение.Сообщить();

		Возврат;

	КонецЕсли;

	ЗаписатьОбщиеНастройкиСинхронизации();
	ЗаписатьНастройкиИПроверитьИзменениеНастроекСинхронизацииКлиент();
	ЗаписатьНастройкиИПроверитьИзменениеНастроекСинхронизацииСервер();
	ЗаписатьНастройкиПередачиФайлов();
	ЗаписатьНастройкиСинхронизацииОбъектов();

	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьНастройкиИПроверитьИзменениеНастроекСинхронизацииСервер()

	ПараметрыСинхронизации = ОбменСМобильнымиСервер.ПолучитьПараметрыСинхронизации(ТекущийПользователь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройкиИПроверитьИзменениеНастроекСинхронизацииКлиент()
		
КонецПроцедуры

#Область ОбщиеНастройкиСинхронизации

&НаСервере
Процедура ЗагрузитьНастройки()

	ЗагрузитьОбщиеНастройкиСинхронизации();
	ЗагрузитьНастройкиПередачиФайлов();
	ЗагрузитьНастройкиСинхронизацииОбъектов();

КонецПроцедуры 

&НаСервере
Процедура ЗагрузитьОбщиеНастройкиСинхронизации()

	МаксимальныйРазмерПередаваемогоФайла = 
		РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ПолучитьНастройку(
			ТекущийПользователь,
			Перечисления.ОбменСМобильнымиТипыНастроекПользователей.МаксимальныйРазмерФайлов);

	МаксимальныйРазмерПередаваемогоФайлаПриОткрытии = МаксимальныйРазмерПередаваемогоФайла;
	
	СрокУстареванияДанных = РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ПолучитьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.СрокУстареванияДанных);

	СрокУстареванияДанныхПриОткрытии = СрокУстареванияДанных;

	ПодробныйПротоколОбменаСУстройством = РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ПолучитьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ПодробныйПротоколОбменаСМобильнымУстройством);

	ПодробныйПротоколОбменаСУстройствомПриОткрытии = ПодробныйПротоколОбменаСУстройством;

КонецПроцедуры 

&НаСервере
Процедура ЗаписатьОбщиеНастройкиСинхронизации()

	РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ЗаписатьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.МаксимальныйРазмерФайлов,
		МаксимальныйРазмерПередаваемогоФайла);

	// todo
	//Если МаксимальныйРазмерПередаваемогоФайлаПриОткрытии <> МаксимальныйРазмерПередаваемогоФайла Тогда
	//	РегистрыСведений.ИзмененныеНастройкиСинхронизацииСМобильнымКлиентом.ДобавитьЗапись(
	//		ТекущийПользователь,
	//		Перечисления.ВидыНастроекОбменаСМобильнымКлиентом.МаксимальныйРазмерФайла);
	//КонецЕсли;

	РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ЗаписатьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.СрокУстареванияДанных,
		СрокУстареванияДанных);

	//Если СрокУстареванияДанных <> СрокУстареванияДанныхПриОткрытии Тогда
	//	// добавление записи об изменившейся настройке срока устаревания данных
	//	РегистрыСведений.ИзмененныеНастройкиСинхронизацииСМобильнымКлиентом.ДобавитьЗапись(
	//		ТекущийПользователь,
	//		Перечисления.ВидыНастроекОбменаСМобильнымКлиентом.СрокУстареванияДанных);
	//КонецЕсли;

	РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ЗаписатьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ПодробныйПротоколОбменаСМобильнымУстройством,
		ПодробныйПротоколОбменаСУстройством);

КонецПроцедуры

#КонецОбласти

#Область НастройкиСинхронизацииФайлов

&НаСервере
Процедура ЗагрузитьНастройкиПередачиФайлов()

	ФильтроватьПередаваемыеНаКлиентФайлы = РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ПолучитьНастройку(
			ТекущийПользователь,
			Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ОграничениеФорматовПередаваемыхФайлов);

	ФорматыПередаваемыхФайлов = ВРег(РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ПолучитьНастройку(
			ТекущийПользователь,
			Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ФорматыПередаваемыхФайлов));

	Элементы.ФорматыПередаваемыхФайлов.Доступность = ФильтроватьПередаваемыеНаКлиентФайлы;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиПередачиФайлов()

	РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ЗаписатьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ОграничениеФорматовПередаваемыхФайлов,
		ФильтроватьПередаваемыеНаКлиентФайлы);

	РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ЗаписатьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ФорматыПередаваемыхФайлов,
		ФорматыПередаваемыхФайлов);

КонецПроцедуры 

#КонецОбласти

#Область НастройкиСинхронизацииОбъектов

&НаСервере
Процедура ЗагрузитьНастройкиСинхронизацииОбъектов()
	
	СинхронизацияСписанийДенежныхСредств = РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ПолучитьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.СинхронизацияСписанийДенежныхСредств);
			
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиСинхронизацииОбъектов()
	
	РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ЗаписатьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.СинхронизацияСписанийДенежныхСредств,
		СинхронизацияСписанийДенежныхСредств);
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

